# Maintainer: Rasmus Steinke <rasi at xssn dot at>
# Contributor: Christian Rebischke

pkgname=teiler-git
pkgver=239.75cd77f
pkgrel=1
pkgdesc="a simple screenshot/screencast tool written in bash"
arch=('any')
url='http://teiler.53280.de'
license=('GPL')
depends=('ffmpeg' 'scrot' 'xclip' 'dzen2-xft-xpm-xinerama-git' 'xininfo-git' 'gtk2')
optdepends=('fb-client:         Upload to paste.xinu.at (or selfhosted filebin)'
            'openssh:           Upload to SSH server'
            'imgur:             Upload images to imgur.com'
            'dmenu2:            Backend for teiler'
            'rofi-git:          Alternative backend for teiler'
            'gtk3:              Image to Clipboard'
            's3cmd:             Upload to s3 server')

options=(!strip)
install="teiler.install"
makedepends=('git')
source=('git+http://git.53280.de/teiler')

pkgver() {
	cd teiler
	printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
  cd ${srcdir}/teiler/imgclip
  make
}  

package() {

  install -D -m755 "${srcdir}/teiler/teiler" \
    "${pkgdir}/usr/bin/teiler"

  install -D -m644 "${srcdir}/teiler/config.example" \
    "${pkgdir}/usr/share/doc/teiler/config.example"

  install -D -m644 "${srcdir}/teiler/dmenurc" \
    "${pkgdir}/usr/share/doc/teiler/dmenurc"

  cd ${srcdir}/teiler/imgclip
  make DESTDIR="$pkgdir" install
}

md5sums=('SKIP')
